import com.diffplug.gradle.spotless.JavaExtension

plugins {
	id "java"
	id "java-library"
	id "maven-publish"
	id 'com.diffplug.spotless' version "6.12.0"
    id 'com.gradleup.shadow' version "9.2.2"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def ENV = System.getenv()
version = "0.6.4" + (ENV.GITHUB_ACTIONS ? "" : "+local")

group = 'net.legacyfabric'
base {
    archivesName = project.name.toLowerCase()
}

repositories {
	mavenCentral()
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
}

configurations {
	ship
	enigma
	implementation.extendsFrom ship
	compileOnly.extendsFrom enigma
	testImplementation.extendsFrom enigma
}

dependencies {
	ship "org.ow2.asm:asm:${asm_version}"
	ship "org.ow2.asm:asm-commons:${asm_version}"
	ship "org.ow2.asm:asm-tree:${asm_version}"
	ship "org.ow2.asm:asm-util:${asm_version}"
	ship 'net.fabricmc:tiny-mappings-parser:0.3.0+build.17'
	implementation 'com.google.guava:guava:33.5.0-jre'
	compileOnly 'org.jetbrains:annotations:20.1.0'
    // https://mvnrepository.com/artifact/org.checkerframework/checker
    implementation 'org.checkerframework:checker:3.51.1'

	enigma 'cuchaz:enigma:0.23.2'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
	format('java', JavaExtension) {
		licenseHeaderFile(rootProject.file("HEADER"))
		target '**/*.java'
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Stitch',
				'Implementation-Version': archiveVersion,
				'Main-Class': "net.fabricmc.stitch.Main"
	}
}

//task allJar(type: Jar) {
//	from {
//		configurations.ship.collect {
//			it.isDirectory() ? it : zipTree(it)
//		}
//	}.setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
//	manifest {
//		attributes 'Implementation-Title': 'Stitch',
//				'Implementation-Version': archiveVersion,
//				'Main-Class': "net.fabricmc.stitch.Main"
//	}
//	archiveClassifier = 'all'
//	with jar
//}

assemble.dependsOn(shadowJar)
shadowJar {
    configurations = [ project.configurations.ship ]
}

java {
	withSourcesJar()
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) it.options.release = 17
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact(shadowJar)
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
		    maven {
				url "https://repo.legacyfabric.net/repository/legacyfabric"
				credentials {
					username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
					password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
				}
				authentication {
					basic(BasicAuthentication)
				}
		    }
		}
	}
}

test {
	useJUnitPlatform()
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def url = new URL("https://repo.legacyfabric.net/repository/legacyfabric/net/legacyfabric/stitch/maven-metadata.xml")
		if (url != null) {
			def xml = url.text
			if (xml != null && !xml.isEmpty()) {
				def metadata = new XmlSlurper().parseText(xml)
				def versions = metadata.versioning.versions.version*.text();
				if (versions.contains(version)) {
					throw new RuntimeException("${version} has already been released!")
				}
			}
		}
	}
}

publish.mustRunAfter checkVersion
